In this step-by-step tutorial series, you will create your first complete 2D game with Godot. By the end of the series, you will have a simple yet complete game of your own, like the image below.

You will learn how the Godot editor works, how to structure a project, and build a 2D game.

> [!Note]
> This project is an introduction to the Godot engine. It assumes that you have some programming experience already. If you're new to programming entirely, you should start here: [Scripting languages](https://docs.godotengine.org/en/stable/getting_started/step_by_step/scripting_languages.html#doc-scripting).

The game is called "Dodge the Creeps!". Your character must move and avoid the enemies for as long as possible.

You will learn to:

-   Create a complete 2D game with the Godot editor.
    
-   Structure a simple game project.
    
-   Move the player character and change its sprite.
    
-   Spawn random enemies.
    
-   Count the score.
    
And more.

You'll find another series where you'll create a similar game but in 3D. We recommend you to start with this one, though.

**Why start with 2D?**

3D games are much more complex than 2D ones. It would be best if you stuck to 2D until you understood the game development process and how to use Godot well.

You can find a completed version of this project at this location:

-   [https://github.com/godotengine/godot-demo-projects](https://github.com/godotengine/godot-demo-projects)
    
## Prerequisites

This step-by-step tutorial is intended for beginners who followed the complete [Getting Started](https://docs.godotengine.org/en/stable/getting_started/step_by_step/index.html#toc-learn-step-by-step).

If you're an experienced programmer, you can find the complete demo's source code here: [Godot demo projects](https://github.com/godotengine/godot-demo-projects).

We prepared some game assets you'll need to download so we can jump straight to the code.

You can download them by clicking the link below.

[`dodge_assets.zip`](https://docs.godotengine.org/en/stable/_downloads/923b18d4e18125cf494ee5f7efba7e03/dodge_assets.zip).

## Contents

-   [Setting up the project]
-   [Creating the player scene]
-   [Coding the player]
-   [Creating the enemy]
-   [The main game scene]
-   [Heads up display]
-   [Finishing up]

# Setting up the project

In this short first part, we'll set up and organize the project.

Launch Godot and create a new project.

![../../_images/new-project-button.png||370](https://docs.godotengine.org/en/stable/_images/new-project-button.png)

**GDScript**
Download [`dodge_assets.zip`](https://docs.godotengine.org/en/stable/_downloads/923b18d4e18125cf494ee5f7efba7e03/dodge_assets.zip). The archive contains the images and sounds you'll be using to make the game. Extract the archive and move the `art/` and `fonts/` directories to your project's directory.

Your project folder should look like this.
![../../_images/folder-content.png||340](https://docs.godotengine.org/en/stable/_images/folder-content.png)

This game is designed for portrait mode, so we need to adjust the size of the game window. Click on _Project -> Project Settings_ to open the project settings window and in the left column, open the _Display -> Window_ tab. There, set "Width" to `480` and "Height" to `720`.

![../../_images/setting-project-width-and-height.png||400](https://docs.godotengine.org/en/stable/_images/setting-project-width-and-height.png)

Also, scroll down to the bottom of the section and, under the "Stretch" options, set `Mode` to "2d" and `Aspect` to "keep". This ensures that the game scales consistently on different sized screens.

![../../_images/setting-stretch-mode.png||400](https://docs.godotengine.org/en/stable/_images/setting-stretch-mode.png)

## Organizing the project

In this project, we will make 3 independent scenes: `Player`, `Mob`, and `HUD`, which we will combine into the game's `Main` scene.

In a larger project, it might be useful to create folders to hold the various scenes and their scripts, but for this relatively small game, you can save your scenes and scripts in the project's root folder, identified by `res://`. You can see your project folders in the FileSystem dock in the lower left corner:

![../../_images/filesystem_dock.png](https://docs.godotengine.org/en/stable/_images/filesystem_dock.png)

With the project in place, we're ready to design the player scene in the next lesson.

# Creating the player scene

With the project settings in place, we can start working on the player-controlled character.

The first scene will define the `Player` object. One of the benefits of creating a separate Player scene is that we can test it separately, even before we've created other parts of the game.

## Node structure

To begin, we need to choose a root node for the player object. As a general rule, a scene's root node should reflect the object's desired functionality - what the object _is_. Click the "Other Node" button and add an [Area2D](https://docs.godotengine.org/en/stable/classes/class_area2d.html#class-area2d) node to the scene.

![../../_images/add_node.png|center|180](https://docs.godotengine.org/en/stable/_images/add_node.png)

Godot will display a warning icon next to the node in the scene tree. You can ignore it for now. We will address it later.

With `Area2D` we can detect objects that overlap or run into the player. Change the node's name to `Player` by double-clicking on it. Now that we've set the scene's root node, we can add additional nodes to give it more functionality.

Before we add any children to the `Player` node, we want to make sure we don't accidentally move or resize them by clicking on them. Select the node and click the icon to the right of the lock; its tooltip says "Makes sure the object's children are not selectable."

![../../_images/lock_children.png](https://docs.godotengine.org/en/stable/_images/lock_children.png)

Save the scene. Click Scene -> Save, or press Ctrl + S on Windows/Linux or Cmd + S on macOS.

> [!note]
> For this project, we will be following the Godot naming conventions.
> -   **GDScript**: Classes (nodes) use PascalCase, variables and functions use snake_case, and constants use ALL_CAPS (See [GDScript style guide](https://docs.godotengine.org/en/stable/tutorials/scripting/gdscript/gdscript_styleguide.html#doc-gdscript-styleguide)).
> -   **C#**: Classes, export variables and methods use PascalCase, private fields use _camelCase, local variables and parameters use camelCase (See [C# style guide](https://docs.godotengine.org/en/stable/tutorials/scripting/c_sharp/c_sharp_style_guide.html#doc-c-sharp-styleguide)). Be careful to type the method names precisely when connecting signals.
    

## Sprite animation

Click on the `Player` node and add an [AnimatedSprite](https://docs.godotengine.org/en/stable/classes/class_animatedsprite.html#class-animatedsprite) node as a child. The `AnimatedSprite` will handle the appearance and animations for our player. Notice that there is a warning symbol next to the node. An `AnimatedSprite` requires a [SpriteFrames](https://docs.godotengine.org/en/stable/classes/class_spriteframes.html#class-spriteframes) resource, which is a list of the animations it can display. To create one, find the `Frames` property in the Inspector and click "[empty]" -> "New SpriteFrames". Click again to open the "SpriteFrames" panel:

![../../_images/spriteframes_panel.png||400](https://docs.godotengine.org/en/stable/_images/spriteframes_panel.png)

On the left is a list of animations. Click the "default" one and rename it to "walk". Then click the "New Animation" button to create a second animation named "up". Find the player images in the "FileSystem" tab - they're in the `art` folder you unzipped earlier. Drag the two images for each animation, named `playerGrey_up[1/2]` and `playerGrey_walk[1/2]`, into the "Animation Frames" side of the panel for the corresponding animation:

![../../_images/spriteframes_panel2.png||300](https://docs.godotengine.org/en/stable/_images/spriteframes_panel2.png)

The player images are a bit too large for the game window, so we need to scale them down. Click on the `AnimatedSprite` node and set the `Scale` property to `(0.5, 0.5)`. You can find it in the Inspector under the `Node2D` heading.

![../../_images/player_scale.png](https://docs.godotengine.org/en/stable/_images/player_scale.png)

Finally, add a [CollisionShape2D](https://docs.godotengine.org/en/stable/classes/class_collisionshape2d.html#class-collisionshape2d) as a child of `Player`. This will determine the player's "hitbox", or the bounds of its collision area. For this character, a `CapsuleShape2D` node gives the best fit, so next to "Shape" in the Inspector, click "[empty]"" -> "New CapsuleShape2D". Using the two size handles, resize the shape to cover the sprite:

![../../_images/player_coll_shape1.png||200](https://docs.godotengine.org/en/stable/_images/player_coll_shape1.png)

When you're finished, your `Player` scene should look like this:

![../../_images/player_scene_nodes.png](https://docs.godotengine.org/en/stable/_images/player_scene_nodes.png)

Make sure to save the scene again after these changes.

In the next part, we'll add a script to the player node to move and animate it. Then, we'll set up collision detection to know when the player got hit by something.

# Coding the player

In this lesson, we'll add player movement, animation, and set it up to detect collisions.

To do so, we need to add some functionality that we can't get from a built-in node, so we'll add a script. Click the `Player` node and click the "Attach Script" button:

![../../_images/add_script_button.png](https://docs.godotengine.org/en/stable/_images/add_script_button.png)

In the script settings window, you can leave the default settings alone. Just click "Create":

> [!note]
> If you're creating a C# script or other languages, select the language from the language drop down menu before hitting create.

![../../_images/attach_node_window.png||200](https://docs.godotengine.org/en/stable/_images/attach_node_window.png)

> [!note]
> If this is your first time encountering GDScript, please read [Scripting languages](https://docs.godotengine.org/en/stable/getting_started/step_by_step/scripting_languages.html#doc-scripting) before continuing.

Start by declaring the member variables this object will need:

```gdscript
extends Area2D

export var speed = 400 # How fast the player will move (pixels/sec).
var screen_size # Size of the game window.
```

Using the `export` keyword on the first variable `speed` allows us to set its value in the Inspector. This can be handy for values that you want to be able to adjust just like a node's built-in properties. Click on the `Player` node and you'll see the property now appears in the "Script Variables" section of the Inspector. Remember, if you change the value here, it will override the value written in the script.

> [!warning]
> If you're using C#, you need to (re)build the project assemblies whenever you want to see new export variables or signals. This build can be manually triggered by clicking the word "Mono" at the bottom of the editor window to reveal the Mono Panel, then clicking the "Build Project" button.

![../../_images/export_variable.png](https://docs.godotengine.org/en/stable/_images/export_variable.png)

The `_ready()` function is called when a node enters the scene tree, which is a good time to find the size of the game window:

```gdscript
func _ready():
    screen_size = get_viewport_rect().size
```

Now we can use the `_process()` function to define what the player will do. `_process()` is called every frame, so we'll use it to update elements of our game, which we expect will change often. For the player, we need to do the following:

-   Check for input.
-   Move in the given direction.
-   Play the appropriate animation.
    
First, we need to check for input - is the player pressing a key? For this game, we have 4 direction inputs to check. Input actions are defined in the Project Settings under "Input Map". Here, you can define custom events and assign different keys, mouse events, or other inputs to them. For this game, we will map the arrow keys to the four directions.

Click on _Project -> Project Settings_ to open the project settings window and click on the _Input Map_ tab at the top. Type "move_right" in the top bar and click the "Add" button to add the `move_right` action.

![../../_images/input-mapping-add-action.png](https://docs.godotengine.org/en/stable/_images/input-mapping-add-action.png)

We need to assign a key to this action. Click the "+" icon on the right, then click the "Key" option in the drop-down menu. A dialog asks you to type in the desired key. Press the right arrow on your keyboard and click "Ok".

![../../_images/input-mapping-add-key.png||500](https://docs.godotengine.org/en/stable/_images/input-mapping-add-key.png)

Repeat these steps to add three more mappings:

1.  `move_left` mapped to the left arrow key.
2.  `move_up` mapped to the up arrow key.
3.  And `move_down` mapped to the down arrow key.
    
Your input map tab should look like this:

![../../_images/input-mapping-completed.png||500](https://docs.godotengine.org/en/stable/_images/input-mapping-completed.png)

Click the "Close" button to close the project settings.

> [!note]
> We only mapped one key to each input action, but you can map multiple keys, joystick buttons, or mouse buttons to the same input action.

You can detect whether a key is pressed using `Input.is_action_pressed()`, which returns `true` if it's pressed or `false` if it isn't.

```gdscript
func _process(delta):
    var velocity = Vector2.ZERO # The player's movement vector.
    if Input.is_action_pressed("move_right"):
        velocity.x += 1
    if Input.is_action_pressed("move_left"):
        velocity.x -= 1
    if Input.is_action_pressed("move_down"):
        velocity.y += 1
    if Input.is_action_pressed("move_up"):
        velocity.y -= 1

    if velocity.length() > 0:
        velocity = velocity.normalized() * speed
        $AnimatedSprite.play()
    else:
        $AnimatedSprite.stop()
```

We start by setting the `velocity` to `(0, 0)` - by default, the player should not be moving. Then we check each input and add/subtract from the `velocity` to obtain a total direction. For example, if you hold `right` and `down` at the same time, the resulting `velocity` vector will be `(1, 1)`. In this case, since we're adding a horizontal and a vertical movement, the player would move _faster_ diagonally than if it just moved horizontally.

We can prevent that if we _normalize_ the velocity, which means we set its _length_ to `1`, then multiply by the desired speed. This means no more fast diagonal movement.

> [!tip]
> If you've never used vector math before, or need a refresher, you can see an explanation of vector usage in Godot at [Vector math](https://docs.godotengine.org/en/stable/tutorials/math/vector_math.html#doc-vector-math). It's good to know but won't be necessary for the rest of this tutorial.

We also check whether the player is moving so we can call `play()` or `stop()` on the AnimatedSprite.

> [!Tip]
> `$` is shorthand for `get_node()`. So in the code above, `$AnimatedSprite.play()` is the same as `get_node("AnimatedSprite").play()`.

In GDScript, `$` returns the node at the relative path from the current node, or returns `null` if the node is not found. Since AnimatedSprite is a child of the current node, we can use `$AnimatedSprite`.

Now that we have a movement direction, we can update the player's position. We can also use `clamp()` to prevent it from leaving the screen. _Clamping_ a value means restricting it to a given range. Add the following to the bottom of the `_process` function (make sure it's not indented under the else):

```gdscript
position += velocity * delta
position.x = clamp(position.x, 0, screen_size.x)
position.y = clamp(position.y, 0, screen_size.y)
```

> [!tip]
> The delta parameter in the _process() function refers to the _frame length_ - the amount of time that the previous frame took to complete. Using this value ensures that your movement will remain consistent even if the frame rate changes.

Click "Play Scene" (F6, Cmd + R on macOS) and confirm you can move the player around the screen in all directions.

> [!Warning]
> If you get an error in the "Debugger" panel that says
> `Attempt to call function 'play' in base 'null instance' on a null instance`
> this likely means you spelled the name of the AnimatedSprite node wrong. Node names are case-sensitive and `$NodeName` must match the name you see in the scene tree.

## Choosing animations

Now that the player can move, we need to change which animation the AnimatedSprite is playing based on its direction. We have the "walk" animation, which shows the player walking to the right. This animation should be flipped horizontally using the `flip_h` property for left movement. We also have the "up" animation, which should be flipped vertically with `flip_v` for downward movement. Let's place this code at the end of the `_process()` function:

```gdscript
if velocity.x != 0:
    $AnimatedSprite.animation = "walk"
    $AnimatedSprite.flip_v = false
    # See the note below about boolean assignment.
    $AnimatedSprite.flip_h = velocity.x < 0
elif velocity.y != 0:
    $AnimatedSprite.animation = "up"
    $AnimatedSprite.flip_v = velocity.y > 0
```

> [!Note]
> The boolean assignments in the code above are a common shorthand for programmers. Since we're doing a comparison test (boolean) and also _assigning_ a boolean value, we can do both at the same time. Consider this code versus the one-line boolean assignment above:

```gdscript
if velocity.x < 0:
    $AnimatedSprite.flip_h = true
else:
    $AnimatedSprite.flip_h = false
```

Play the scene again and check that the animations are correct in each of the directions.

> [!Tip]
> A common mistake here is to type the names of the animations wrong. The animation names in the SpriteFrames panel must match what you type in the code. If you named the animation `"Walk"`, you must also use a capital "W" in the code.

When you're sure the movement is working correctly, add this line to `_ready()`, so the player will be hidden when the game starts:

```gdscript
hide()
```

## Preparing for collisions

We want `Player` to detect when it's hit by an enemy, but we haven't made any enemies yet! That's OK, because we're going to use Godot's _signal_ functionality to make it work.

Add the following at the top of the script, after `extends Area2D`:
```gdscript
signal hit
```

This defines a custom signal called "hit" that we will have our player emit (send out) when it collides with an enemy. We will use `Area2D` to detect the collision. Select the `Player` node and click the "Node" tab next to the Inspector tab to see the list of signals the player can emit:

![../../_images/player_signals.png||250](https://docs.godotengine.org/en/stable/_images/player_signals.png)

Notice our custom "hit" signal is there as well! Since our enemies are going to be `RigidBody2D` nodes, we want the `body_entered(body: Node)` signal. This signal will be emitted when a body contacts the player. Click "Connect.." and the "Connect a Signal" window appears. We don't need to change any of these settings so click "Connect" again. Godot will automatically create a function in your player's script.

![ signal | center | 512](https://docs.godotengine.org/en/stable/_images/player_signal_connection.png)

Note the green icon indicating that a signal is connected to this function. Add this code to the function:

```gdscript
func _on_Player_body_entered(body):
    hide() # Player disappears after being hit.
    emit_signal("hit")
    # Must be deferred as we can't change physics properties on a physics callback.
    $CollisionShape2D.set_deferred("disabled", true)
```

Each time an enemy hits the player, the signal is going to be emitted. We need to disable the player's collision so that we don't trigger the `hit` signal more than once.

> [!Note]
> Disabling the area's collision shape can cause an error if it happens in the middle of the engine's collision processing. Using `set_deferred()` tells Godot to wait to disable the shape until it's safe to do so.

The last piece is to add a function we can call to reset the player when starting a new game.

```gdscript
func start(pos):
    position = pos
    show()
    $CollisionShape2D.disabled = false
```

With the player working, we'll work on the enemy in the next lesson.

# Creating the enemy

Now it's time to make the enemies our player will have to dodge. Their behavior will not be very complex: mobs will spawn randomly at the edges of the screen, choose a random direction, and move in a straight line.

We'll create a `Mob` scene, which we can then _instance_ to create any number of independent mobs in the game.

## Node setup

Click Scene -> New Scene and add the following nodes:

-   [RigidBody2D](https://docs.godotengine.org/en/stable/classes/class_rigidbody2d.html#class-rigidbody2d) (named `Mob`)    
    > -   [AnimatedSprite](https://docs.godotengine.org/en/stable/classes/class_animatedsprite.html#class-animatedsprite)
    > -   [CollisionShape2D](https://docs.godotengine.org/en/stable/classes/class_collisionshape2d.html#class-collisionshape2d)
    > -   [VisibilityNotifier2D](https://docs.godotengine.org/en/stable/classes/class_visibilitynotifier2d.html#class-visibilitynotifier2d)

Don't forget to set the children so they can't be selected, like you did with the Player scene.

In the [RigidBody2D](https://docs.godotengine.org/en/stable/classes/class_rigidbody2d.html#class-rigidbody2d) properties, set `Gravity Scale` to `0`, so the mob will not fall downward. In addition, under the [CollisionObject2D](https://docs.godotengine.org/en/stable/classes/class_collisionobject2d.html#class-collisionobject2d) section, click the `Mask` property and uncheck the first box. This will ensure the mobs do not collide with each other.

![../../_images/set_collision_mask.png](https://docs.godotengine.org/en/stable/_images/set_collision_mask.png)

Set up the [AnimatedSprite](https://docs.godotengine.org/en/stable/classes/class_animatedsprite.html#class-animatedsprite) like you did for the player. This time, we have 3 animations: `fly`, `swim`, and `walk`. There are two images for each animation in the art folder.

Adjust the "Speed (FPS)" to `3` for all animations.

![../../_images/mob_animations.gif](https://docs.godotengine.org/en/stable/_images/mob_animations.gif)

Set the `Playing` property in the Inspector to "On".

We'll select one of these animations randomly so that the mobs will have some variety.

Like the player images, these mob images need to be scaled down. Set the `AnimatedSprite`'s `Scale` property to `(0.75, 0.75)`.

As in the `Player` scene, add a `CapsuleShape2D` for the collision. To align the shape with the image, you'll need to set the `Rotation Degrees` property to `90` (under "Transform" in the Inspector).

Save the scene.

## Enemy script

Add a script to the `Mob` like this:

```gdscript
extends RigidBody2D
```

Now let's look at the rest of the script. In `_ready()` we play the animation and randomly choose one of the three animation types:

```gdscript
func _ready():
    $AnimatedSprite.playing = true
    var mob_types = $AnimatedSprite.frames.get_animation_names()
    $AnimatedSprite.animation = mob_types[randi() % mob_types.size()]
```

First, we get the list of animation names from the AnimatedSprite's `frames` property. This returns an Array containing all three animation names: `["walk", "swim", "fly"]`.

We then need to pick a random number between `0` and `2` to select one of these names from the list (array indices start at `0`). `randi() % n` selects a random integer between `0` and `n-1`.

> [!Note]
> You must use `randomize()` if you want your sequence of "random" numbers to be different every time you run the scene. We're going to use `randomize()` in our `Main` scene, so we won't need it here.

The last piece is to make the mobs delete themselves when they leave the screen. Connect the `screen_exited()` signal of the `VisibilityNotifier2D` node and add this code:

## Tabs
```gdscript
func _on_VisibilityNotifier2D_screen_exited():
    queue_free()
```

This completes the Mob scene.

With the player and enemies ready, in the next part, we'll bring them together in a new scene. We'll make enemies spawn randomly around the game board and move forward, turning our project into a playable game.

# The main game scene

Now it's time to bring everything we did together into a playable game scene.

Create a new scene and add a [Node](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node) named `Main`. (The reason we are using Node instead of Node2D is because this node will be a container for handling game logic. It does not require 2D functionality itself.)

Click the **Instance** button (represented by a chain link icon) and select your saved `Player.tscn`.

![../../_images/instance_scene.png](https://docs.godotengine.org/en/stable/_images/instance_scene.png)

Now, add the following nodes as children of `Main`, and name them as shown (values are in seconds):

-   [Timer](https://docs.godotengine.org/en/stable/classes/class_timer.html#class-timer) (named `MobTimer`) - to control how often mobs spawn
-   [Timer](https://docs.godotengine.org/en/stable/classes/class_timer.html#class-timer) (named `ScoreTimer`) - to increment the score every second
-   [Timer](https://docs.godotengine.org/en/stable/classes/class_timer.html#class-timer) (named `StartTimer`) - to give a delay before starting
-   [Position2D](https://docs.godotengine.org/en/stable/classes/class_position2d.html#class-position2d) (named `StartPosition`) - to indicate the player's start position
    
Set the `Wait Time` property of each of the `Timer` nodes as follows:

-   `MobTimer`: `0.5`
-   `ScoreTimer`: `1`
-   `StartTimer`: `2`
    
In addition, set the `One Shot` property of `StartTimer` to "On" and set `Position` of the `StartPosition` node to `(240, 450)`.

## Spawning mobs

The Main node will be spawning new mobs, and we want them to appear at a random location on the edge of the screen. Add a [Path2D](https://docs.godotengine.org/en/stable/classes/class_path2d.html#class-path2d) node named `MobPath` as a child of `Main`. When you select `Path2D`, you will see some new buttons at the top of the editor:

![../../_images/path2d_buttons.png](https://docs.godotengine.org/en/stable/_images/path2d_buttons.png)

Select the middle one ("Add Point") and draw the path by clicking to add the points at the corners shown. To have the points snap to the grid, make sure "Use Grid Snap" and "Use Snap" are both selected. These options can be found to the left of the "Lock" button, appearing as a magnet next to some dots and intersecting lines, respectively.

![../../_images/grid_snap_button.png](https://docs.godotengine.org/en/stable/_images/grid_snap_button.png)

> [!Important]
> Draw the path in _clockwise_ order, or your mobs will spawn pointing _outwards_ instead of _inwards_!

![||250](https://docs.godotengine.org/en/stable/_images/draw_path2d.gif)

After placing point `4` in the image, click the "Close Curve" button and your curve will be complete.

Now that the path is defined, add a [PathFollow2D](https://docs.godotengine.org/en/stable/classes/class_pathfollow2d.html#class-pathfollow2d) node as a child of `MobPath` and name it `MobSpawnLocation`. This node will automatically rotate and follow the path as it moves, so we can use it to select a random position and direction along the path.

Your scene should look like this:

![../../_images/main_scene_nodes.png||200](https://docs.godotengine.org/en/stable/_images/main_scene_nodes.png)

## Main script

Add a script to `Main`. At the top of the script, we use `export (PackedScene)` to allow us to choose the Mob scene we want to instance.

```gdscript
extends Node

export(PackedScene) var mob_scene
var score
```

We also add a call to `randomize()` here so that the random number generator generates different random numbers each time the game is run:

```gdscript
func _ready():
    randomize()
```

Click the `Main` node and you will see the `Mob Scene` property in the Inspector under "Script Variables".

>You can assign this property's value in two ways:
>-   Drag `Mob.tscn` from the "FileSystem" dock and drop it in the **Mob Scene** property.
>-   Click the down arrow next to "[empty]" and choose "Load". Select `Mob.tscn`.
    
Next, select the `Player` node in the Scene dock, and access the Node dock on the sidebar. Make sure to have the Signals tab selected in the Node dock.

You should see a list of the signals for the `Player` node. Find and double-click the `hit` signal in the list (or right-click it and select "Connect..."). This will open the signal connection dialog. We want to make a new function named `game_over`, which will handle what needs to happen when a game ends. Type "game_over" in the "Receiver Method" box at the bottom of the signal connection dialog and click "Connect". Add the following code to the new function, as well as a `new_game` function that will set everything up for a new game:

```gdscript
func game_over():
    $ScoreTimer.stop()
    $MobTimer.stop()

func new_game():
    score = 0
    $Player.start($StartPosition.position)
    $StartTimer.start()
```

Now connect the `timeout()` signal of each of the Timer nodes (`StartTimer`, `ScoreTimer` , and `MobTimer`) to the main script. `StartTimer` will start the other two timers. `ScoreTimer` will increment the score by 1.

```gdscript
func _on_ScoreTimer_timeout():
    score += 1

func _on_StartTimer_timeout():
    $MobTimer.start()
    $ScoreTimer.start()
```

In `_on_MobTimer_timeout()`, we will create a mob instance, pick a random starting location along the `Path2D`, and set the mob in motion. The `PathFollow2D` node will automatically rotate as it follows the path, so we will use that to select the mob's direction as well as its position. When we spawn a mob, we'll pick a random value between `150.0` and `250.0` for how fast each mob will move (it would be boring if they were all moving at the same speed).

Note that a new instance must be added to the scene using `add_child()`.

```gdscript
func _on_MobTimer_timeout():
    # Create a new instance of the Mob scene.
    var mob = mob_scene.instance()

    # Choose a random location on Path2D.
    var mob_spawn_location = get_node("MobPath/MobSpawnLocation")
    mob_spawn_location.offset = randi()

    # Set the mob's direction perpendicular to the path direction.
    var direction = mob_spawn_location.rotation + PI / 2

    # Set the mob's position to a random location.
    mob.position = mob_spawn_location.position

    # Add some randomness to the direction.
    direction += rand_range(-PI / 4, PI / 4)
    mob.rotation = direction

    # Choose the velocity for the mob.
    var velocity = Vector2(rand_range(150.0, 250.0), 0.0)
    mob.linear_velocity = velocity.rotated(direction)

    # Spawn the mob by adding it to the Main scene.
    add_child(mob)
```

> [!Important]
> Why `PI`? In functions requiring angles, Godot uses _radians_, not degrees. Pi represents a half turn in radians, about `3.1415` (there is also `TAU` which is equal to `2 * PI`). If you're more comfortable working with degrees, you'll need to use the `deg2rad()` and `rad2deg()` functions to convert between the two.

## Testing the scene

Let's test the scene to make sure everything is working. Add this `new_game` call to `_ready()`:

```gdscript
func _ready():
    randomize()
    new_game()
```

```c#
public override void _Ready() {
    NewGame();
}
```

Let's also assign `Main` as our "Main Scene" - the one that runs automatically when the game launches. Press the "Play" button and select `Main.tscn` when prompted.

> [!Tip]
> If you had already set another scene as the "Main Scene", you can right click `Main.tscn` in the FileSystem dock and select "Set As Main Scene".

You should be able to move the player around, see mobs spawning, and see the player disappear when hit by a mob.

When you're sure everything is working, remove the call to `new_game()` from `_ready()`.

What's our game lacking? Some user interface. In the next lesson, we'll add a title screen and display the player's score.

# Heads up display

The final piece our game needs is a User Interface (UI) to display things like score, a "game over" message, and a restart button.

Create a new scene, and add a [CanvasLayer](https://docs.godotengine.org/en/stable/classes/class_canvaslayer.html#class-canvaslayer) node named `HUD`. "HUD" stands for "heads-up display", an informational display that appears as an overlay on top of the game view.

The [CanvasLayer](https://docs.godotengine.org/en/stable/classes/class_canvaslayer.html#class-canvaslayer) node lets us draw our UI elements on a layer above the rest of the game, so that the information it displays isn't covered up by any game elements like the player or mobs.

The HUD needs to display the following information:

-   Score, changed by `ScoreTimer`.
-   A message, such as "Game Over" or "Get Ready!"
-   A "Start" button to begin the game.
    
The basic node for UI elements is [Control](https://docs.godotengine.org/en/stable/classes/class_control.html#class-control). To create our UI, we'll use two types of [Control](https://docs.godotengine.org/en/stable/classes/class_control.html#class-control) nodes: [Label](https://docs.godotengine.org/en/stable/classes/class_label.html#class-label) and [Button](https://docs.godotengine.org/en/stable/classes/class_button.html#class-button).

Create the following as children of the `HUD` node:

-   [Label](https://docs.godotengine.org/en/stable/classes/class_label.html#class-label) named `ScoreLabel`.
-   [Label](https://docs.godotengine.org/en/stable/classes/class_label.html#class-label) named `Message`.
-   [Button](https://docs.godotengine.org/en/stable/classes/class_button.html#class-button) named `StartButton`.
-   [Timer](https://docs.godotengine.org/en/stable/classes/class_timer.html#class-timer) named `MessageTimer`.
    
Click on the `ScoreLabel` and type a number into the `Text` field in the Inspector. The default font for `Control` nodes is small and doesn't scale well. There is a font file included in the game assets called "Xolonium-Regular.ttf". To use this font, do the following:

1.  Under **Theme overrides > Fonts** click on the empty box and select "New DynamicFont"

![../../_images/custom_font1.png](https://docs.godotengine.org/en/stable/_images/custom_font1.png)

2.  Click on the "DynamicFont" you added, and under **Font > FontData**, choose "Load" and select the "Xolonium-Regular.ttf" file.
    

![../../_images/custom_font2.png](https://docs.godotengine.org/en/stable/_images/custom_font2.png)

Set the "Size" property under `Settings`, `64` works well.

![../../_images/custom_font3.png](https://docs.godotengine.org/en/stable/_images/custom_font3.png)

Once you've done this on the `ScoreLabel`, you can click the down arrow next to the Font property and choose "Copy", then "Paste" it in the same place on the other two Control nodes.

> [!Note]
> **Anchors and Margins:** `Control` nodes have a position and size, but they also have anchors and margins. Anchors define the origin - the reference point for the edges of the node. Margins update automatically when you move or resize a control node. They represent the distance from the control node's edges to its anchor.

Arrange the nodes as shown below. Click the "Layout" button to set a Control node's layout:

![||450](https://docs.godotengine.org/en/stable/_images/ui_anchor.png)

You can drag the nodes to place them manually, or for more precise placement, use the following settings:

## ScoreLabel

-   _Layout_ : "Top Wide"
-   _Text_ : `0`
-   _Align_ : "Center"
    
## Message

-   _Layout_ : "HCenter Wide"
-   _Text_ : `Dodge the Creeps!`
-   _Align_ : "Center"
-   _Autowrap_ : "On"
    
## StartButton

-   _Text_ : `Start`
-   _Layout_ : "Center Bottom"
-   _Margin_ :
    -   Top: `-200`
    -   Bottom: `-100`
        
On the `MessageTimer`, set the `Wait Time` to `2` and set the `One Shot` property to "On".

Now add this script to `HUD`:

```gdscript
extends CanvasLayer

signal start_game
```

The `start_game` signal tells the `Main` node that the button has been pressed.

```gdscript
func show_message(text):
    $Message.text = text
    $Message.show()
    $MessageTimer.start()
```

This function is called when we want to display a message temporarily, such as "Get Ready".

```gdscript
func show_game_over():
    show_message("Game Over")
    # Wait until the MessageTimer has counted down.
    yield($MessageTimer, "timeout")

    $Message.text = "Dodge the\nCreeps!"
    $Message.show()
    # Make a one-shot timer and wait for it to finish.
    yield(get_tree().create_timer(1), "timeout")
    $StartButton.show()
```

This function is called when the player loses. It will show "Game Over" for 2 seconds, then return to the title screen and, after a brief pause, show the "Start" button.

> [!Note]
> When you need to pause for a brief time, an alternative to using a Timer node is to use the SceneTree's `create_timer()` function. This can be very useful to add delays such as in the above code, where we want to wait some time before showing the "Start" button.

```gdscript
func update_score(score):
    $ScoreLabel.text = str(score)
```

This function is called by `Main` whenever the score changes.

Connect the `timeout()` signal of `MessageTimer` and the `pressed()` signal of `StartButton` and add the following code to the new functions:

```gdscript
func _on_StartButton_pressed():
    $StartButton.hide()
    emit_signal("start_game")

func _on_MessageTimer_timeout():
    $Message.hide()
```

## Connecting HUD to Main

Now that we're done creating the `HUD` scene, go back to `Main`. Instance the `HUD` scene in `Main` like you did the `Player` scene. The scene tree should look like this, so make sure you didn't miss anything:

![../../_images/completed_main_scene.png||170](https://docs.godotengine.org/en/stable/_images/completed_main_scene.png)

Now we need to connect the `HUD` functionality to our `Main` script. This requires a few additions to the `Main` scene:

In the Node tab, connect the HUD's `start_game` signal to the `new_game()` function of the Main node by typing "new_game" in the "Receiver Method" in the "Connect a Signal" window. Verify that the green connection icon now appears next to `func new_game()` in the script.

In `new_game()`, update the score display and show the "Get Ready" message:

```gdscript
$HUD.update_score(score)
$HUD.show_message("Get Ready")
```

In `game_over()` we need to call the corresponding `HUD` function:

```gdscript
$HUD.show_game_over()
```

Finally, add this to `_on_ScoreTimer_timeout()` to keep the display in sync with the changing score:

```gdscript
$HUD.update_score(score)
```

Now you're ready to play! Click the "Play the Project" button. You will be asked to select a main scene, so choose `Main.tscn`.

## Removing old creeps

If you play until "Game Over" and then start a new game right away, the creeps from the previous game may still be on the screen. It would be better if they all disappeared at the start of a new game. We just need a way to tell _all_ the mobs to remove themselves. We can do this with the "group" feature.

In the `Mob` scene, select the root node and click the "Node" tab next to the Inspector (the same place where you find the node's signals). Next to "Signals", click "Groups" and you can type a new group name and click "Add".

![../../_images/group_tab.png||200](https://docs.godotengine.org/en/stable/_images/group_tab.png)

Now all mobs will be in the "mobs" group. We can then add the following line to the `new_game()` function in `Main`:

```gdscript
get_tree().call_group("mobs", "queue_free")
```

The `call_group()` function calls the named function on every node in a group - in this case we are telling every mob to delete itself.

The game's mostly done at this point. In the next and last part, we'll polish it a bit by adding a background, looping music, and some keyboard shortcuts.

# Finishing up

We have now completed all the functionality for our game. Below are some remaining steps to add a bit more "juice" to improve the game experience.

Feel free to expand the gameplay with your own ideas.

## Background

The default gray background is not very appealing, so let's change its color. One way to do this is to use a [ColorRect](https://docs.godotengine.org/en/stable/classes/class_colorrect.html#class-colorrect) node. Make it the first node under `Main` so that it will be drawn behind the other nodes. `ColorRect` only has one property: `Color`. Choose a color you like and select "Layout" -> "Full Rect" so that it covers the screen.

You could also add a background image, if you have one, by using a `TextureRect` node instead.

## Sound effects

Sound and music can be the single most effective way to add appeal to the game experience. In your game assets folder, you have two sound files: "House In a Forest Loop.ogg" for background music, and "gameover.wav" for when the player loses.

Add two [AudioStreamPlayer](https://docs.godotengine.org/en/stable/classes/class_audiostreamplayer.html#class-audiostreamplayer) nodes as children of `Main`. Name one of them `Music` and the other `DeathSound`. On each one, click on the `Stream` property, select "Load", and choose the corresponding audio file.

To play the music, add `$Music.play()` in the `new_game()` function and `$Music.stop()` in the `game_over()` function.

Finally, add `$DeathSound.play()` in the `game_over()` function.

## Keyboard shortcut

Since the game is played with keyboard controls, it would be convenient if we could also start the game by pressing a key on the keyboard. We can do this with the "Shortcut" property of the `Button` node.

In a previous lesson, we created four input actions to move the character. We will create a similar input action to map to the start button.

Select "Project" -> "Project Settings" and then click on the "Input Map" tab. In the same way you created the movement input actions, create a new input action called `start_game` and add a key mapping for the Enter key.

In the `HUD` scene, select the `StartButton` and find its _Shortcut_ property in the Inspector. Select "New Shortcut" and click on the "Shortcut" item. A second _Shortcut_ property will appear. Select "New InputEventAction" and click the new "InputEventAction". Finally, in the _Action_ property, type the name `start_game`.

![../../_images/start_button_shortcut.png||300](https://docs.godotengine.org/en/stable/_images/start_button_shortcut.png)

Now when the start button appears, you can either click it or press Enter to start the game.

And with that, you completed your first 2D game in Godot.

![../../_images/dodge_preview.gif](https://docs.godotengine.org/en/stable/_images/dodge_preview.gif)

You got to make a player-controlled character, enemies that spawn randomly around the game board, count the score, implement a game over and replay, user interface, sounds, and more. Congratulations!

There's still much to learn, but you can take a moment to appreciate what you achieved.

And when you're ready, you can move on to [Your first 3D game](https://docs.godotengine.org/en/stable/getting_started/first_3d_game/index.html#doc-your-first-3d-game) to learn to create a complete 3D game from scratch, in Godot.